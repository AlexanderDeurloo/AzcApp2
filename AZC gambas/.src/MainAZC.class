' Gambas class file

Public LogFile As File  ' used to catch "PRINT" after it is redirected via "LogFile = OPEN ... " + "OUTPUT TO LogFile" 

'PUBLIC LevelBit AS Boolean
Public Protect As Boolean
Public starttime As Float
Public overalltime As Integer
Public Time_Correction As Integer
Public NoGo As Boolean

Public Proc As Integer
Public FileString As String
Public Max_Proc As Integer
Public ReceivedData As String
Public Bowl_Dirty As Boolean = True
Public Stirrer_Dirty As Boolean = True
Public OperatorChoose As String = "nop"
Public Clean_Every_Startup_Flag As Boolean

' [GB2:ARRD] Public Var_Array As String[100, 3]
Public Var_Array As New String[100, 3]
Public Var_Ini As Integer = 0    ' first colomn
Public Var_Recipe As Integer = 1 ' second colomn


Public Max_Input_Containers As Integer = 1
Public Max_Output_Containers As Integer = 2
Public MaxBowlFreq As Integer = 3
Public Default_Recipe As Integer = 4
Public Default_Directory As Integer = 5
Public Kaoline_Clean_Time As Integer = 6
Public Motor_ACC_Time As Integer = 7    'used to calculate the total process time
Public Motor_DEC_Time As Integer = 8    'used to calculate the total process time
Public Tube_Clean_Time As Integer = 9
Public Clean_Every_Startup As Integer = 10
Public Timeout_Water_Fill_Valve As Integer = 11
Public Timeout_Input_Store_Rotate As Integer = 12 'china
Public Stirrer_Low_Speed As Integer = 13 'china
Public Stirrer_High_Speed As Integer = 14 'china
Public CommPortPlc As Integer = 15 'china
Public CommPortStirrer As Integer = 16 'china
Public Auto_Shutdown As Integer = 17
Public SensorsViaBus As Integer = 18

Public Stir_Time As Integer = 1
Public Magnesium_Fill_Time As Integer = 2
Public Bowl_Water_Fill_Time As Integer = 3
Public Sample_Time As Integer = 4
Public Kaoline_Fill_Time As Integer = 5
Public Centrifuge_Time As Integer = 6
Public Output_Capture_Time As Integer = 7
Public Stirrer_Clean_Time As Integer = 8
Public Bowl_Clean_Phase1_Time As Integer = 9
Public Bowl_Clean_Phase2_Time As Integer = 10
Public Bowl_Clean_Phase3_Time As Integer = 11
Public Bowl_Clean_Pause_Time As Integer = 12

Public cancel As Integer
Public pause As Integer
Public CONFIRM As Boolean = False
Public StopProcess As Integer = 0     ' controlled by stop button and processctrl.
Public DumpI As Integer = 0

Public Ini_Path As String = "~/Settings/AZC.ini"

'Public ArmedForShutdown As Boolean

Private ProgressBarDrawArea As DrawingArea

Public Sub GetFileName(FullPath As String) As String
  
  Dim PathLength As Integer
  Dim FileName As String
  
  PathLength = Len(Var_Array[Default_Directory, Var_Ini])
  FileName = Mid$(FullPath, PathLength + 1, 30) ' for now 30 is the max length of the filename
  
  Return FileName
End



Public Sub DA_Draw()
 Dim pb As String
 Dim i As Integer
 
 pb = CStr(ProgressBar1.Value * 100) & "%"
 i = ProgressBarDrawArea.Font.TextWidth(pb)
 With Paint
   .Brush = .Color(&H244B96)  
   .Rectangle(0, 0, ProgressBarDrawArea.W * ProgressBar1.Value, ProgressBarDrawArea.H)
   .Fill
   '.Brush = .Color(Color.Black)
   '.DrawText(pb, (ProgressBarDrawArea.W / 2) - (i / 2), ProgressBarDrawArea.H / 2, .Font.TextWidth(pb), ProgressBarDrawArea.H / 2, Align.Center)
   .End
 End With
End








Public Sub Menu_Home_About_Click()
  Dim AboutMessage As String
  AboutMessage = "Automated Zonal Centrifuge.\nMediQuip\nThe Netherlands\nwww.AutomatedZonalCentrifuge.com"
  Message.Title = "About"
  Message.info(AboutMessage)
End


Public Sub Menu_Home_Exit_Click()
  Dim KeyPass As String
  KeyPass = InputBox("Enter key to exit application", "Authorized exit only")
  If KeyPass <> "123456"
   Message.Title = "Attention"
   Message.Error("Wrong key")
   Return
  Endif
  StatusBox.Text = "Exit authorized...please wait"
  If nogo = False Then StirProcess.MoveStir("clean")
  ' no shutdown, just ending the application.
  Wait 1
  '  ArmedForShutdown = False
  ' copy Exit script:
  Try Kill "/home/pi/AzcSystem/Finalize.sh"
  Copy "/home/pi/AzcSystem/Exit.sh" To "/home/pi/AzcSystem/Finalize.sh"
  
  Me.Close
End


Public Sub Menu_Home_Shutdown_Click()
   Dim returncode As Integer
  Message.Title = "Attention"
  returncode = Message.Question("Shutdown machine?", "Yes, I'm sure", "No, go back please") ' returns 1 for button yes, 2 for button 2 etc.
  If returncode = 2 Then
    Return
  Endif

  If nogo = False Then StirProcess.MoveStir("clean")

  Wait 1
' IF (Var_Array[Auto_Shutdown, Var_Ini] = "1") THEN 
  StatusBox.Text = "Machine will switch off automatically."
  Print " auto down"
  
'  ArmedForShutdown = True
  ' copy shutdwon script:
  Try Kill "/home/pi/AzcSystem/Finalize.sh"
  Copy "/home/pi/AzcSystem/Shutdown.sh" To "/home/pi/AzcSystem/Finalize.sh"

  Me.Close
End


Public Sub form_Close()
 ' StatusBox.Text = "shutting down..."
  SerialCpuComm.TxRxFast("blue", 1)
  
  CleanMachine.Magnesium(3)

  SerialCpuComm.TxRxFast("blinkoff", 1)
  FreqCtrl.SetFreq(0.0, "OFF")
  Stirrer.SetRpm(0)
  Stirrer.CloseComm()
  StatusBox.Text = "Closing program..."
  PlcOutputs.Set("WaterValveHpPump", 0) 
  PlcOutputs.Set("AirBowlCooling", 0)
  PlcOutputs.Set("KaolineMotorPower", 0)

  PlcOutputs.Set("ShutdownIndicator", 1)  ' relay will be open.
  Wait 1
  Print "Close Form"
  Close LogFile
  
 ' If ArmedForShutdown = True
  ' SerialCpuComm.TxRxFast("shutdown", 0)
  '  Wait 1
  'Endif

End


Public Sub Form_Open()
  Dim Start_Recipe As String
  
  LogFile = Open "/home/pi/AzcApp/Log/DebugOutput" For Write Create ' TODO: use date code
  Output To LogFile
  
  NoGo = True
  Stirrer_Dirty = False ' debug : save time
  Bowl_Dirty = False  ' debug : save time
  
  protect = True                ' locks menu items
  Set_To_Disable()

  Me.Maximized = True

  ProgressElapsedTimer.Delay = 1000   ' 1 sec interval
  ProgressElapsedTimer.Enabled = False

  Proc = 0
  
  Read_File(Ini_Path, Var_Ini)    ' load ini file   ' temporary stored on the server. Find better location.
  Start_Recipe = Var_Array[Default_Directory, Var_Ini] & Var_Array[Default_Recipe, Var_Ini]
  Read_File(Start_Recipe, Var_Recipe)  ' load recipe
  Use_Recipe()
  
  Me.Text = GetFileName(Start_Recipe)
  
  StartButton.Enabled = False


  StopButton.Enabled = False

  StartButton.SetFocus()
  MainDelay.Delay = 1000       ' lowest working value is 9.
  MainDelay.Enabled = True  ' arms timer. On expire the leds are drawn.
  StartButton.Picture = Picture["play.png"]
  Me.Font.Size = 12


  ProgressBarDrawArea = ProgressBar1.Children[0]
  ProgressBarDrawArea.Background = &HF5F5F5
  Object.Attach(ProgressBarDrawArea, Me, "DA")
    
End


Public Sub Init_Machine() As Boolean
    Dim fillcmd As String
   ' ArmedForShutdown = False

    PlcOutputs.Set("UsbHub", 1) ' just for safety: hub is already enabled by startup script.
    Wait 0.5
    PlcOutputs.Set("ShutdownIndicator", 0)  ' relay will be closed.
    Wait 0.5
    PlcOutputs.Set("StirrerPower", 1) ' turns on monitor as well

    Led.Control(Led_MagLevel, "busy")
    Led.Control(Led_KaolineLevel, "busy")
    Led.Control(Led_BowlClean, "busy")
    Led.Control(Led_StirrerClean, "busy")
    led.Control(Led_SystemError, "busy")
    IfmSensors.Init()
    StatusBox.Text = "Plc ready..."
    Wait 1
    Print "let's connect to Cpu"  ' debugger
    ReceivedData = ""
    If (SerialCpuComm.Init(Var_Array[CommPortPlc, Var_Ini]))
      Print "Error in initializing serialcpu\n" 
      Quit
    Endif
    ReceivedData = SerialCpuComm.TxRxFast("PlcPresent", 0)
    If Comp(ReceivedData, "CpuPresent") <> 0
      Print "Cpu didn't reply properly: " & ReceivedData
      ' give it another shot:
      StatusBox.Text = "Cpu pending..."
      Print "Wait until light show is done. 1."
      Wait 2  ' at least 4 sec to end the light show.
      ReceivedData = SerialCpuComm.TxRxFast("PlcPresent", 0)
      If Comp(ReceivedData, "CpuPresent") <> 0
        Print "Cpu didn't reply properly: " & ReceivedData
        StatusBox.Text = "Cpu reset..."
        Wait 1
        Print "Reset CPU..."
          SerialCpuComm.Reset(Var_Array[CommPortPlc, Var_Ini])   
        Print "reset done."
        Wait 5
        ReceivedData = SerialCpuComm.TxRxFast("PlcPresent", 0)
        Wait 5
        If Comp(ReceivedData, "CpuPresent") <> 0
          ReceivedData = SerialCpuComm.TxRxFast("PlcPresent", 0)  ' send again
          If Comp(ReceivedData, "CpuPresent") <> 0
                  Message.Title = "Attention"
                  Message.Error("No contact with Cpu", "Stop")
                  Quit
          Endif
        Endif
      Endif
    Endif
    Print "Wait until light show is done. 2."
    Wait 2 ' at least 4 sec to end the light show.
    ReceivedData = SerialCpuComm.TxRxFast("PlcPresent", 1)
    If Comp(ReceivedData, "CpuPresent") <> 0
      Message.Title = "Attention"
      Message.Error("No proper contact with Cpu", "Stop")
      Quit
    Endif
    Print "Cpu replied properly: " & ReceivedData
    StatusBox.Text = "Cpu ready..."
    SerialCpuComm.TxRxFast("blue", 1)
    SerialCpuComm.TxRxFast("blinkon", 1)
    Wait 1
    FreqCtrl.SetFreq(1.0, "OFF")

    Print " first tx done"
  
    PlcOutputs.Shutdown("off")  ' off = relay closed.
    
    led.Control(Led_SystemError, "off")

    nogo = False
    
    If Check.Magnesium_Level() = "true" Then
      Led.Control(Led_MagLevel, "on")
    Else 
      Led.Control(Led_MagLevel, "error")
    Endif
    If Check.Kaoline_Level() = "true" Then
      Led.Control(Led_KaolineLevel, "on")
    Else 
      Led.Control(Led_KaolineLevel, "error")
    Endif
    Clean_Every_Startup_Flag = False
    
    If Var_Array[Clean_Every_Startup, Var_Ini] = 1 Then Clean_Every_Startup_Flag = True
    Print "clean flag = "
    If Clean_Every_Startup_Flag = False Then
      Print "false"
      Stirrer_Dirty = False
      Bowl_Dirty = False
      Led.Control(Led_BowlClean, "on")
      Led.Control(Led_StirrerClean, "on")
    Endif
    
    If Clean_Every_Startup_Flag = True Then
      Print "true"
      Stirrer_Dirty = True
      Bowl_Dirty = True
      Led.Control(Led_BowlClean, "error")
      Led.Control(Led_StirrerClean, "error")
    Endif
    Wait 1
    PlcOutputs.Set("WaterValveHpPump", 1) ' since water buffer of HP can be empty at startup
    ' generate command for PLC to inform about the waterfill timeout.
    fillcmd = "waterfilltimeout " & (Var_Array[Timeout_Water_Fill_Valve, Var_Ini])
    ReceivedData = SerialCpuComm.TxRxFast(fillcmd, 1)
    Print ReceivedData

    Wait 1           
    PlcOutputs.Set("AirBowlCooling", 1)
    Wait 1
    PlcOutputs.Set("KaolineMotorPower", 1)

    Wait 1
    'Stirrer.OpenComm((Var_Array[CommPortStirrer, Var_Ini]))
    SerialCpuComm.TxRxFast("blinkoff", 1)
    Set_To_Stop()
    StatusBox.Text = "Machine ready"
    StartButton.Enabled = True

End


Public Sub Process_Start()
    Dim i As Integer
    Dim Reply As String
    StopProcess = 0
    Print "Process start"
    SerialCpuComm.TxRxFast("blinkoff", 1)
    SerialCpuComm.TxRxFast("green", 1)
    Wait 1
    StatusText("Checking levels..")
    Wait 1
    ProcessCTRL.Bowl_Stopped = True
  ' check Mag AND kao levels
    If Check.Magnesium_Level() = "true" Then
      Led.Control(Led_MagLevel, "on")
    Else
      Led.Control(Led_MagLevel, "error")
      SerialCpuComm.TxRxFast("red", 1)
      Message.Title = "Attention"
      Message.Error("Magnesium Level too low")
      Goto Finish
    Endif
    If Check.Kaoline_Level() = "true" Then
      Led.Control(Led_KaolineLevel, "on")
    Else
      Led.Control(Led_KaolineLevel, "error")
      SerialCpuComm.TxRxFast("red", 1)
      Message.Title = "Attention"
      Message.Error("Kaoline Level too low")
      Goto Finish
    Endif
    
    StatusText("Checking Kaoline...")
    Wait 1
    If Check.Kaoline_Motor() = "false" Then
      SerialCpuComm.TxRxFast("red", 1)
      Message.Title = "Attention"
      Message.Error("Kaoline motor not connected")
      Goto Finish
    Endif
    
    If PlcInputs.Read("StirrerCoverClosed") = 0 Then
      SerialCpuComm.TxRxFast("red", 1)
      Message.Title = "Attention"
      Message.Error("Stirrer rear cover not closed.")
      Goto Finish
    Endif

  ' synchronize machine
    StatusText("Synchronize machine..")
    i = SyncMachine.Go()
    Print "end sync: " & i
    If (i = 2) Then Goto Finish

    Wait 1
    Print "still processing "
    ' search for input container
    StatusText("Looking for containers..")
    Reply = Search.Input_Container()
    If Reply = "ERR" Then
      SerialCpuComm.TxRxFast("red", 1)
      Message.Title = "Attention"
      Message.Error("No container at input detected")
      Goto Finish   ' was return
    Else If Reply = "TimeOut" Then
      SerialCpuComm.TxRxFast("red", 1)
      Message.Title = "Attention"
      Message.Error("Timeout during input container search")
      Goto Finish   ' was return
    Endif
    If StopProcess = 1 Then Goto finish
    ProcessCTRL.Input_Container = True
    ' search for output container
    If Search.Output_Container() = "ERR" Then
      SerialCpuComm.TxRxFast("red", 1)
      Message.Title = "Attention"
      Message.Error("No container at output detected")
      Goto Finish   ' was return
    Endif
        ' check if output container is empty:
     IfmSensors.Read("OutputLevel")
     Print "NotEmptyCriteria = " & IfmSensors.NotEmptyCriteria
     If (IfmSensors.ResultSensorOutput < IfmSensors.NotEmptyCriteria) Then
      Print "Output container not empty. Read = " & IfmSensors.ResultSensorOutput
      Message.Title = "Attention"
      Message.Error("First output container not empty.", "Stop")
      Wait 1
      Goto finish
     Endif

    If StopProcess = 1 Then Goto finish
    ProcessCTRL.Output_Container = True
    

  Processes.Text = 0    ' reset counter
  ProgressBar1.Value = 0
  ProcessCTRL.Go()  ' the actual process
 
Finish:
  Print "finished"
  ' in case of stop button:
  If StopProcess = 1 Then
    Stop_Process()
  Else
    SerialCpuComm.TxRxFast("green", 1)
    SerialCpuComm.TxRxFast("blinkon", 1)
  Endif
  Wait 0.5
  Set_To_Stop() ' buttons, etc
  Message.Title = "Done"
  Message.Info("Process finished")
  SerialCpuComm.TxRxFast("blue", 1)
  SerialCpuComm.TxRxFast("blinkoff", 1)
  StatusBox.Text = "Ready"
  Return
End

Public Sub Stop_Process()     ' called after a function if "stop" was pressed.
  SerialCpuComm.TxRxFast("white", 1)
  SerialCpuComm.TxRxFast("blinkon", 1)

  CleanMachine.STOP_TIMERS()
  ProcessCTRL.STOP_TIMERS()
  ProcessCTRL.Stop_All()

  
  
  Wait 0.5
  ' display message with consequences
  Message.Title = "Attention"
  Message.Error("Machine could be in an undefined state now.\nPlease check input and output containers.", "Done")
  MainAZC.StopProcess = 0
End

Public Sub Increment_Counter(cnt As Integer)
  Processes.Text = cnt
End




Public Sub Read_File(FileName As String, StoreColomn As Integer)
  Dim dmp1 As String
  Dim dmp2 As Integer
  Dim pos As Integer
  Print FileName
  FileString = File.Load(FileName)

  For dmp2 = 1 To 20 Step 1
    pos = InStr(FileString, "\n")
    dmp1 = Left(FileString, pos - 1)
    FileString = Right(FileString, - pos)
    Var_Array[dmp2, StoreColomn] = dmp1
  Next
End

Public Sub StartButton_Click()
    If Nogo = True Then Return
    OperatorChoose = "start"
    Set_To_Run()      'set buttons
    Process_Start()
End


Public Sub StopButton_Click()
  Set_To_Disable()          ' disable all buttons
  StatusBox.text = "Stopping machine...  Please wait..."
  OperatorChoose = "stop"   ' global indicator
  StopProcess = 1   ' indicates a request to stop (captured by processCTRL)
  SerialCpuComm.TxRxFast("purple", 1)
  SerialCpuComm.TxRxFast("blinkon", 1)

End



Public Sub Set_To_Disable()   ' disable all buttons. To Do: disable all menus
  protect = False
  Menu_Recipes_Unlock_Click()
  Menu_Home.Enabled = False
  Menu_Recipes.Enabled = False
  Menu_Adv.Enabled = False
  
  StartButton.Enabled = False
  StartButton.ToolTip = ""
  StopButton.Enabled = False
  StopButton.ToolTip = ""
  ProgressElapsedTimer.Enabled = False
  ShutdownButton.Enabled = False
End


Public Sub Set_To_Run()   ' buttons related
  protect = False
  Menu_Recipes_Unlock_Click()
  Menu_Home.Enabled = False
  Menu_Recipes.Enabled = False
  Menu_Adv.Enabled = False
  ShutdownButton.Enabled = False
  
  StartButton.Enabled = False
  StartButton.ToolTip = ""


  StopButton.Enabled = True
  StopButton.ToolTip = "Clicking aborts whole process. Samples will be lost."

End


Public Sub Set_To_Stop()    ' buttons related
  protect = False
  Menu_Recipes_Unlock_Click()
  Menu_Home.Enabled = True
  Menu_Recipes.Enabled = True
  Menu_Adv.Enabled = True
  ShutdownButton.Enabled = True
  StartButton.Enabled = True
  StartButton.ToolTip = "Click to RUN"

  StopButton.Enabled = False
  StopButton.ToolTip = ""
  ProgressElapsedTimer.Enabled = False
  StatusBox.Text = "STOPPED"
  
  ProgressBar1.Value = 0
End


Public Sub Menu_Recipes_Unlock_Click()   ' arms timer. On expire the leds are drawn.

  If Protect = False Then
    SP_StirTime.Enabled = False
    SP_MagFillTime.Enabled = False
    SP_BowlWaterFillTime.Enabled = False
    SP_SampleTime.Enabled = False
    SP_KaolineFillTime.Enabled = False
    SP_CentrifugeTime.Enabled = False
    SP_OutputCaptureTime.Enabled = False
    SP_StirrerCleanTime.Enabled = False
    SP_BowlCleanPhase1Time.Enabled = False
    SP_BowlCleanPhase2Time.Enabled = False
    SP_BowlCleanPhase3Time.Enabled = False
    SP_BowlCleanPauseTime.Enabled = False
    PlusMinEnable(False)    

    Menu_Recipes_Default.Enabled = False
    protect = True
    Menu_Recipes_Unlock.Text = "Unlock"
  Else
    SP_StirTime.Enabled = True
    SP_MagFillTime.Enabled = True
    SP_BowlWaterFillTime.Enabled = True
    SP_SampleTime.Enabled = True
    SP_KaolineFillTime.Enabled = True
    SP_CentrifugeTime.Enabled = True
    SP_OutputCaptureTime.Enabled = True

    SP_StirrerCleanTime.Enabled = True
    SP_BowlCleanPhase1Time.Enabled = True
    SP_BowlCleanPhase2Time.Enabled = True
    SP_BowlCleanPhase3Time.Enabled = True
    SP_BowlCleanPauseTime.Enabled = True
    PlusMinEnable(True)    

    Menu_Recipes_Default.Enabled = True
    protect = False
   
    Menu_Recipes_Unlock.Text = "Lock"
  Endif
End


Public Sub ProgressElapsedTimer_Timer()
  Dim elapsed As Integer
  Dim currenttime As Float
  
  currenttime = Timer()
  elapsed = currenttime - starttime ' seconds so far
  
  ProgressBar1.Value = 1 - ((overalltime - elapsed) / overalltime)
End


Public Sub CTRL_Progress_Timer(state As Boolean)
  starttime = Timer()
  ProgressElapsedTimer.Enabled = state
End


Public Sub Menu_Recipes_Load_Click()
  Dialog.Path = Var_Array[Default_Directory, Var_Ini]
  Dialog.Filter = ["*.rcp", "Recipe Files"]
  If Dialog.OpenFile() Then Return
  Read_File(Dialog.Path, Var_Recipe)
  MainAZC.text = GetFileName(Dialog.Path)                   ' modify the header of the screen, so the user can see which file is used.
  protect = False
  Menu_Recipes_Unlock_Click()
  Use_Recipe()
  Catch
    Message.Title = "Attention"
    Message.Info(Error.Text)
End


Public Sub Menu_Recipes_Save_Click()
  Dim StringToStore As String
  Dim ext As String
  StringToStore = SP_StirTime.Value & "\n" &
                  SP_MagFillTime.Value & "\n" &
                  SP_BowlWaterFillTime.Value & "\n" &
                  SP_SampleTime.Value & "\n" &
                  SP_KaolineFillTime.Value & "\n" &
                  SP_CentrifugeTime.Value & "\n" &
                  SP_OutputCaptureTime.Value & "\n" &
                  SP_StirrerCleanTime.Value & "\n" &
                  SP_BowlCleanPhase1Time.Value & "\n" &
                  SP_BowlCleanPhase2Time.Value & "\n" &
                  SP_BowlCleanPhase3Time.Value & "\n" &
                  SP_BowlCleanPauseTime.Value & "\n"
  
  Dialog.Path = MainAZC.Var_Array[Default_Directory, Var_Ini]
  Print "path is " & Dialog.Path
  Dialog.Filter = ["*.rcp", "Recipe Files"]
  If Dialog.SaveFile() Then Return
  Print "file name is " & Dialog.Path
    ext = Right(Dialog.Path, 4)
  Print "ext = " & ext
  If ext = ".rcp" Then
    'ext is okay.
    Print "file name is okay"
  Else
    'add missing ext:
    Dialog.Path = Dialog.Path & ".rcp"
  Endif
  File.Save(Dialog.Path, StringToStore)
  MainAZC.text = GetFileName(Dialog.Path)                   ' modify the header of the screen, so the user can see which file is used.
  protect = False
  Menu_Recipes_Unlock_Click()
  Catch
    Message.Title = "Attention"
    Message.Info(Error.Text)
End

Public Sub Use_Recipe()
  SP_StirTime.Value = Val(Var_Array[Stir_Time, Var_Recipe])
  SP_MagFillTime.Value = Val(Var_Array[Magnesium_Fill_Time, Var_Recipe])
  SP_BowlWaterFillTime.Value = Val(Var_Array[Bowl_Water_Fill_Time, Var_Recipe])
  SP_SampleTime.Value = Val(Var_Array[Sample_Time, Var_Recipe])
  SP_KaolineFillTime.Value = Val(Var_Array[Kaoline_Fill_Time, Var_Recipe])
  SP_CentrifugeTime.Value = Val(Var_Array[Centrifuge_Time, Var_Recipe])
  SP_OutputCaptureTime.Value = Val(Var_Array[Output_Capture_Time, Var_Recipe])
  
  SP_StirrerCleanTime.Value = Val(Var_Array[Stirrer_Clean_Time, Var_Recipe])
  SP_BowlCleanPhase1Time.Value = Val(Var_Array[Bowl_Clean_Phase1_Time, Var_Recipe])
  SP_BowlCleanPhase2Time.Value = Val(Var_Array[Bowl_Clean_Phase2_Time, Var_Recipe])
  SP_BowlCleanPhase3Time.Value = Val(Var_Array[Bowl_Clean_Phase3_Time, Var_Recipe])
  SP_BowlCleanPauseTime.Value = Val(Var_Array[Bowl_Clean_Pause_Time, Var_Recipe])
  
End


Public Sub Menu_Recipes_Default_Click()
  Dim StringToStore As String
  Dim New_Default_Recipe As String
  Dim name_start As Integer
  Dialog.Path = Var_Array[Default_Directory, Var_Ini]
  Dialog.Filter = ["*.rcp", "Recipe Files"]
  If Dialog.OpenFile() Then Return
  name_start = Len(Var_Array[Default_Directory, Var_Ini])
  New_Default_Recipe = Mid(Dialog.Path, name_start + 1)
  Print New_Default_Recipe
  StringToStore = Var_Array[Max_Input_Containers, Var_Ini] & "\n" &
                  Var_Array[Max_Output_Containers, Var_Ini] & "\n" &
                  Var_Array[MaxBowlFreq, Var_Ini] & "\n" &
                  New_Default_Recipe & "\n" &
                  Var_Array[Default_Directory, Var_Ini] & "\n" &
                  Var_Array[Kaoline_Clean_Time, Var_Ini] & "\n" &
                  Var_Array[Motor_ACC_Time, Var_Ini] & "\n" &
                  Var_Array[Motor_DEC_Time, Var_Ini] & "\n" &
                  Var_Array[Tube_Clean_Time, Var_Ini] & "\n" &
                  Var_Array[Clean_Every_Startup, Var_Ini] & "\n" &
                  Var_Array[Timeout_Water_Fill_Valve, Var_Ini] & "\n" &
                  Var_Array[Timeout_Input_Store_Rotate, Var_Ini] & "\n" & 'china
                  Var_Array[Stirrer_Low_Speed, Var_Ini] & "\n" & 'china
                  Var_Array[Stirrer_High_Speed, Var_Ini] & "\n" & 'china
                  Var_Array[CommPortPlc, Var_Ini] & "\n" & 'china
                  Var_Array[CommPortStirrer, Var_Ini] & "\n" & 'china
                  Var_Array[Auto_Shutdown, Var_Ini] & "\n" &
                  Var_Array[SensorsViaBus, Var_Ini] & "\n"
                  
  File.Save(Ini_Path, StringToStore)
  protect = False
  Menu_Recipes_Unlock_Click()
End

Public Function DrawLeds()
  Led.Control(Led_MagLevel, "off")
  Led.Control(Led_KaolineLevel, "off")
  Led.Control(Led_StirrerClean, "off")
  Led.Control(Led_BowlClean, "off")
  Led.Control(Led_SystemError, "off")
End

Public Function Update_LEDs()
    If Stirrer_Dirty = False Then
        Led.Control(Led_StirrerClean, "on")
    Else
      If CleanMachine.Stir_Clean_Pending = False Then
        Led.Control(Led_StirrerClean, "error")
      Else
        Led.Control(Led_StirrerClean, "busy")
      Endif
    Endif
    
    If Bowl_Dirty = False Then
      Led.Control(Led_BowlClean, "on")
    Else
      If CleanMachine.Bowl_Clean_Pending = False Then
        Led.Control(Led_BowlClean, "error")
      Else
        Led.Control(Led_BowlClean, "busy")
      Endif
    Endif
End


Public Sub MainDelay_Timer() ' used to create event. Otherwise I didn't succeed to draw the leds at start.
  MainDelay.Enabled = False
  DrawLeds()
'  Progress.Display(BarArea, 0)
  ProgressBar1.Value = 0
  Init_Machine()
End



Public Sub Menu_Adv_Clean_Stirrer_Click()
  Set_To_Disable()
  SerialCpuComm.TxRxFast("green", 1)
  SerialCpuComm.TxRxFast("blinkoff", 1)
  StatusBox.Text = "Cleaning Stirrer"
  Led.Control(Led_StirrerClean, "busy")
  Print "setup stir clean"
  MainAZC.Fill_Time_Table() ' loads all timer settings in variables.
  
  'CleanMachine.Stir_Clean_Pending = FALSE
  CleanMachine.Stir_Clean_Setup = False
  CleanMachine.Stir(TimeTable.Stirrer_Clean_Time)
  MainAZC.update_leds()
  Print "waiting via .all"
  CleanMachine.All(True)  ' true means: skip bowl

  Led.Control(Led_StirrerClean, "on")
  SerialCpuComm.TxRxFast("blinkon", 1)
  Message.Title = "Finished"
  Message.Info("Stirrer clean done.")
  SerialCpuComm.TxRxFast("blinkoff", 1)
  SerialCpuComm.TxRxFast("blue", 1)
  Set_To_Stop()
  StatusBox.Text = "Ready"
End


Public Sub Menu_Adv_Clean_Bowl_Click()
  Set_To_Disable()
  SerialCpuComm.TxRxFast("green", 1)
  SerialCpuComm.TxRxFast("blinkoff", 1)
  StatusBox.Text = "Cleaning Bowl"
  Led.Control(Led_BowlClean, "busy")
  Print "setup bowl clean"
  MainAZC.Fill_Time_Table() ' loads all timer settings in variables.
  MainAZC.update_leds()
  ProcessCTRL.Bowl_Stopped = True
  CleanMachine.Bowl_Setup()
  Print "setup bowl done"
  Led.Control(Led_BowlClean, "on")
  SerialCpuComm.TxRxFast("blinkon", 1)
  Message.Title = "Finished"
  Message.Info("Bowl clean done.")
  SerialCpuComm.TxRxFast("blinkoff", 1)
  SerialCpuComm.TxRxFast("blue", 1)
  Set_To_Stop()
  StatusBox.Text = "Ready"
End



Public Sub Fill_Time_Table()
  TimeTable.Stir_Time = SP_StirTime.Value * 1000
  TimeTable.Magnesium_Fill_Time = SP_MagFillTime.Value * 1000
  TimeTable.Bowl_Water_Fill_Time = SP_BowlWaterFillTime.Value * 1000
  TimeTable.Sample_Time = SP_SampleTime.Value * 1000
  TimeTable.Kaoline_Fill_Time = SP_KaolineFillTime.Value * 1000
  TimeTable.Centrifuge_Time = SP_CentrifugeTime.Value * 1000
  TimeTable.Output_Capture_Time = (SP_OutputCaptureTime.Value * 1000) + (Val(Var_Array[Motor_DEC_Time, Var_Ini]) * 1000) ' new : capture time in the eyes of the user is the value in the spinbox and recipe file.

  TimeTable.Stirrer_Clean_Time = SP_StirrerCleanTime.Value * 1000
  TimeTable.Bowl_Clean_Phase1_Time = SP_BowlCleanPhase1Time.Value * 1000
  TimeTable.Bowl_Clean_Phase2_Time = SP_BowlCleanPhase2Time.Value * 1000
  TimeTable.Bowl_Clean_Phase3_Time = SP_BowlCleanPhase3Time.Value * 1000
  TimeTable.Bowl_Clean_Pause_Time = SP_BowlCleanPauseTime.Value * 1000
  
  TimeTable.Kaoline_Valve_Clean_Time = Val(Var_Array[Kaoline_Clean_Time, Var_Ini]) '* 100 'china: added 1000 to make sec
  TimeTable.Motor_ACC = Val(Var_Array[Motor_ACC_Time, Var_Ini]) * 1000
  TimeTable.Motor_DEC = Val(Var_Array[Motor_DEC_Time, Var_Ini]) * 1000
  TimeTable.Tube_Clean = Val(Var_Array[Tube_Clean_Time, Var_Ini]) * 1000
  
  overalltime = (SP_StirTime.Value + SP_MagFillTime.Value + SP_BowlWaterFillTime.Value + SP_SampleTime.Value + SP_KaolineFillTime.Value + SP_CentrifugeTime.Value + SP_OutputCaptureTime.Value + Val(Var_Array[Motor_ACC_Time, Var_Ini]) + 150) ' 150 seconds is for movements
  Print "overalltime = " & overalltime
  
End

Public Sub StatusText(mess As String)
  StatusBox.Text = mess
End


Public Sub Menu_Adv_Clean_Sample_Filter_Click()
  Set_To_Disable()
  SerialCpuComm.TxRxFast("yellow", 1)
  StatusBox.Text = "Manual clean sample filter"
  Led.Control(Led_BowlClean, "busy")
  Print "setup manual sample filter clean"
  MainAZC.update_leds()
  StirProcess.MoveStirCleanFilter()
  Wait 2
  Message.Title = ""
  Message.Question("Done?")
  Print "manual sample filter clean done"
  StirProcess.MoveStir("clean")
  SerialCpuComm.TxRxFast("blue", 1)
  Set_To_Stop()
  StatusBox.Text = "Ready"
End



Public Sub PlusMinEnable(Setting As Boolean)
    Stir_Min.Enabled = Setting
    Stir_Plus.Enabled = Setting
    MagnFill_Min.Enabled = Setting
    MagnFill_Plus.Enabled = Setting
    BowlWaterFill_Min.Enabled = Setting
    BowlWaterFill_Plus.Enabled = Setting
    Sample_Min.Enabled = Setting
    Sample_Plus.Enabled = Setting
    Kaoline_Min.Enabled = Setting
    Kaoline_Plus.Enabled = Setting
    BowlWaterFill_Min.Enabled = Setting
    BowlWaterFill_Plus.Enabled = Setting
    Centrifuge_Min.Enabled = Setting
    Centrifuge_Plus.Enabled = Setting
    OutputCapture_Min.Enabled = Setting
    OutputCapture_Plus.Enabled = Setting
    StirClean_Min.Enabled = Setting
    StirClean_Plus.Enabled = Setting
    BowlCleanPhase1_Min.Enabled = Setting
    BowlCleanPhase1_Plus.Enabled = Setting
    BowlCleanPhase2_Min.Enabled = Setting
    BowlCleanPhase2_Plus.Enabled = Setting
    BowlCleanPhase3_Min.Enabled = Setting
    BowlCleanPhase3_Plus.Enabled = Setting
    BowlCleanPause_Min.Enabled = Setting
    BowlCleanPause_Plus.Enabled = Setting
End


Public Sub Stir_Min_Click()
  Dec SP_StirTime.value
End
Public Sub Stir_Plus_Click()
  Inc SP_StirTime.value
End

Public Sub MagnFill_Min_Click()
  Dec SP_MagFillTime.Value
End
Public Sub MagnFill_Plus_Click()
  Inc SP_MagFillTime.Value
End

Public Sub BowlWaterFill_Min_Click()
  Dec SP_BowlWaterFillTime.Value
End
Public Sub BowlWaterFill_Plus_Click()
  Inc SP_BowlWaterFillTime.Value
End

Public Sub Sample_Min_Click()
  Dec SP_SampleTime.Value
End
Public Sub Sample_Plus_Click()
  Inc SP_SampleTime.Value
End

Public Sub Kaoline_Min_Click()
  Dec SP_KaolineFillTime.Value
End
Public Sub Kaoline_Plus_Click()
  Inc SP_KaolineFillTime.Value
End

Public Sub Centrifuge_Min_Click()
  Dec SP_CentrifugeTime.Value
End
Public Sub Centrifuge_Plus_Click()
  Inc SP_CentrifugeTime.Value
End

Public Sub OutputCapture_Min_Click()
  Dec SP_OutputCaptureTime.Value
End
Public Sub OutputCapture_Plus_Click()
  Inc SP_OutputCaptureTime.Value
End

Public Sub StirClean_Min_Click()
  Dec SP_StirrerCleanTime.Value
End
Public Sub StirClean_Plus_Click()
  Inc SP_StirrerCleanTime.Value
End

Public Sub BowlCleanPhase1_Min_Click()
  Dec SP_BowlCleanPhase1Time.Value
End
Public Sub BowlCleanPhase1_Plus_Click()
  Inc SP_BowlCleanPhase1Time.Value
End

Public Sub BowlCleanPhase2_Min_Click()
  Dec SP_BowlCleanPhase2Time.Value
End
Public Sub BowlCleanPhase2_Plus_Click()
  Inc SP_BowlCleanPhase2Time.Value
End

Public Sub BowlCleanPhase3_Min_Click()
  Dec SP_BowlCleanPhase3Time.Value
End
Public Sub BowlCleanPhase3_Plus_Click()
  Inc SP_BowlCleanPhase3Time.Value
End
Public Sub BowlCleanPause_Min_Click()
  Dec SP_BowlCleanPauseTime.Value
End
Public Sub BowlCleanPause_Plus_Click()
  Inc SP_BowlCleanPauseTime.Value
End


Public Sub ShutdownButton_Click()
  Menu_Home_Shutdown_Click()
End


Public Sub Menu_Adv_ControlSampleValve_Click()
  Dim KeyPress As Integer
  Message.Title = "Valve"
  Do
    KeyPress = Message.Question("Sample", "Open", "Close", "Exit")
    If KeyPress = 1
      PlcOutputs.Set("AirTubeValveSample", 1)
    Else If (KeyPress = 2)
      PlcOutputs.Set("AirTubeValveSample", 0)
    Endif
  Loop Until KeyPress = 3
  PlcOutputs.Set("AirTubeValveSample", 0)
End

Public Sub Menu_Adv_ControlMagnesiumValve_Click()
  Dim KeyPress As Integer
  Message.Title = "Valve"
  Do
    KeyPress = Message.Question("Magnesium", "Open", "Close", "Exit")
    If KeyPress = 1
      PlcOutputs.Set("AirTubeValveMagnesium", 1)
    Else If (KeyPress = 2)
      PlcOutputs.Set("AirTubeValveMagnesium", 0)
    Endif
  Loop Until KeyPress = 3
  PlcOutputs.Set("AirTubeValveMagnesium", 0)
End

Public Sub Menu_Adv_ControlKaolineValve_Click()
  Dim KeyPress As Integer
  Message.Title = "Valve"
  Do
    KeyPress = Message.Question("Kaoline", "Open", "Close", "Exit")
    If KeyPress = 1
      PlcOutputs.Set("AirTubeValveKaoline", 1)
    Else If (KeyPress = 2)
      PlcOutputs.Set("AirTubeValveKaoline", 0)
    Endif
  Loop Until KeyPress = 3
  PlcOutputs.Set("AirTubeValveKaoline", 0)
End

Public Sub Menu_Adv_CleanMagnesium_Click()

  Message.Title = "Attention"
  Message.Info("Remove magnesium nozzle from container and place into empty beaker to capture 120cc")
  CleanMachine.Magnesium(10)
End


